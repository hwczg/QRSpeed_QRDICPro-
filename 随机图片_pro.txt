//作者：文瑶 <2370927759>
//安卓版本 <6.0.1> 、<QRDICPro 1.2.1> 版本运行。
//本词库未经测试，如有bug之处请见谅。
//采用 <GNU General Public License v3.0> 共享。

随机图片菜单。
╭ 随机图片菜单 ─┄
a:["上传图片 + 图片","随机图片","^_^ 授予@***1-15级权限"]
b:0
c:$JSON 长度 a$
:文瑶
如果:%b%>=%c%
±rep %Msgbar%±±strmsg ——文瑶BOT。\%0AQQ：237097759±
╰ 啦啦啦啦啦啦……
返回
如果尾
├ [%b%+1]：$JSON 获取 a %b%$。\r
b:[%b%+1]
$跳 :文瑶$

授予@.+ (\d+)级?权限。
如果:%QQ%==2370927759
$写 .文瑶/.数据库/随机图片/权限/%AT0%  %括号1%$
╭ 随机图片分词库 ─┄\r
a:$读 .文瑶/.数据库/随机图片/权限/%AT0%  0$
如果:%a%==%括号1%
├ 授予成功！\r
如果尾
如果:%a%!=%括号1%
├ 授予失败…\r
如果尾
╰ $回调 运行时间$ ─┄

(上传|随机)图片
$禁 %群号% %QQ% 1$
╭ 随机图片分词库 ─┄\r
├ 哼\r
├ 句号呐？？？\r
├╭(╯^╰)╮\r
╰ $回调 运行时间$ ─┄

上传图片。
╭ 随机图片分词库 ─┄\r
如果:%IMG0%==
├ 提示：请附带图片！！！\r
╰ $回调 运行时间$ ─┄
返回
如果尾
s:%IMGNUM%
x:0
:文瑶
如果:%x%>=%s%
├ 提示：请等待审核。\r
╰ $回调 运行时间$ ─┄
返回
如果尾
a:$执行 \%25IMG%x%\%25$
b:$图片链接 %a%$
$下载 /storage/emulated/0/QR/QRDic/data/.文瑶/.数据库/随机图片/上传图片/%群号%-%QQ%-%a% %b%$
z:$访问 file:///storage/emulated/0/QR/QRDic/data/.文瑶/.数据库/随机图片/上传图片/%群号%-%QQ%-%a%$
如果:%z%==
x:[%x%+1]
├ %x%：上传失败！\r
$跳 :文瑶$
如果尾
x:[%x%+1]
├ %x%：上传成功！\r
$跳 :文瑶$

随机图片。
╭ 随机图片分词库 ─┄\r
如果:$读 .文瑶/.数据库/随机图片/冷却/%群号%  0$>[%NDTime%-5000]
├ 冷却中！\r
╰ $回调 运行时间$ ─┄
返回
如果尾
j:$读 .文瑶/.数据库/随机图片/权限/%QQ%  1$
j:12
k:$随机数 0-%j%$
路:/storage/emulated/0/QR/QRDic/库/.图片库/二次元图（可爱）/%k%
a:$访问 file://%路%$
换:$URLDecoder %0A$
a:$取中间 ◎ %a%◎<hr>%换%◎$
a:["$替换 ◎ %a%◎<br>%换%◎","$◎"]
b:$JSON 长度 a$
b:$随机数 0-[%b%-2]$
├ 图片均来源于网络 / 上传。\r
├ 如有不妥之处，请联系作者。\r
├─────┬─────┬┄\r
±img=%路%/@a[%b%]±\r
├─────┴─────┴┄\r
├ 想上传图片？\r
├ 发送 < 上传图片。>\r
├ 并附带一张图片就可以咯！\r
│ \r
├ 您目前权限%j%级！！！\r
╰ $回调 运行时间$ ─┄
$写 .文瑶/.数据库/随机图片/冷却/%群号%  %NDTime%$

[内部]运行时间。?
a:[%NDTime%-%RobotRunTime%]
天:0
时:0
分:0
秒:0
毫:0
如果:%a%>86400000
天:[%a%/86400000]
a:[%a%-(%天%*86400000)]
如果尾
如果:%a%>3600000
时:[%a%/3600000]
a:[%a%-(%时%*3600000)]
如果尾
如果:%a%>60000
分:[%a%/60000]
a:[%a%-(%分%*60000)]
如果尾
如果:%a%>1000
秒:[%a%/1000]
a:[%a%-(%秒%*1000)]
如果尾
如果:%a%>0
毫:[%a%]
如果尾
运行：%天%天%时%时%分%分%秒%秒

[\s\S]*
如果:$随机数 0-100$==50
路:/storage/emulated/0/QR/QRDic/库/.图片库/二次元表情包
a:$访问 file://%路%$
换:$URLDecoder %0A$
a:$取中间 ◎ %a%◎<hr>%换%◎$
a:["$替换 ◎ %a%◎<br>%换%◎","$◎"]
b:$JSON 长度 a$
b:$随机数 0-[%b%-2]$
±img=%路%/@a[%b%]±