//作者：文瑶 <2370927759>
//安卓版本 <6.0.1> 、<QRDICPro 1.2.1> 版本运行。
//本词库未经测试，如有bug之处请见谅。
//采用 <GNU General Public License v3.0> 共享。

(?i)留言板。|lh(elp)?\.
文瑶（2370927759）制作。\r
a:$读 .文瑶/.数据库/留言板/存在留言id  []$
b:0
:文瑶
如果:%b%<$JSON 长度 a$
c:$JSON 获取 a %b%$
d:$读 .文瑶/.数据库/留言板/留言库/%c%  默认值$
d:$JSON 获取 d 留言内容$
留言id：$JSON 获取 a %b%$\r
$回调 留言取开头%d%$\r
b:[%b%+1]
$跳 :文瑶$
如果尾
\r
$回调 留言板help$

[内部]留言取开头(.{1,5})(.*)
%括号1%
如果:%括号2%!=
……
返回
如果尾
返回

(?i)留言板(help|菜单)[\.。]
HELP\r
指令：\r
< 写留言 + 内容 >（100字以内）。\r
< 查留言 + id。>

写留言 *(.{1,100})。
a:$读 .文瑶/.数据库/留言板/已有留言id  0$
b:$读 .文瑶/.数据库/留言板/存在留言id  []$
c:{}
$JSON 添加 b [%a%+1]$
$写 .文瑶/.数据库/留言板/已有留言id  [%a%+1]$
$写 .文瑶/.数据库/留言板/存在留言id  %b%$
$JSON 添加 c 留言人 %QQ%$
$JSON 添加 c 留言群号 %群号%$
$JSON 添加 c 留言内容 %括号1%$
$JSON 添加 c 留言时间 %NDTime%$
$写 .文瑶/.数据库/留言板/留言库/[%a%+1]  %c%$
留言成功！

查留言 *([0-9]+)。
如果:%QQ%!=%主人%&%QQ%!=$管理员 %QQ%$&%QQ%!=2370927759
你没有查看的权限！
返回
如果尾
a:$读 .文瑶/.数据库/留言板/留言库/%括号1%  空！$
b:$读 .文瑶/.数据库/留言板/存在留言id  []$
c:0
:文瑶
如果:%c%<$JSON 长度 b$
如果:$JSON 获取 b %c%$==%括号1%
留言内容：$JSON 获取 a 留言内容$
$JSON 删除 b %c%$
$写 .文瑶/.数据库/留言板/存在留言id  %b%$
返回
如果尾
c:[%c%+1]
$跳 :文瑶$
如果尾
未检索到值！